# Copyright 2021 Fabio D'Urso. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

group("stardock") {
  deps = [
    ":stardock_cli",
    ":stardock_package",
    "//src/proc/bin/starnix",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":stardock-tests" ]
}

rustc_library("common") {
  name = "stardock_common"
  edition = "2018"
  with_unit_tests = true

  source_root = "common/lib.rs"
  deps = [
    "//sdk/fidl/fuchsia.stardock:fuchsia.stardock-rustc",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:regex",
  ]

  test_deps = [
    "//src/lib/fuchsia",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:test-case",
  ]

  sources = [
    "common/digest.rs",
    "common/lib.rs",
  ]
}

fuchsia_unittest_component("stardock-common-tests") {
  deps = [ ":common_test" ]
}

rustc_binary("manager_bin") {
  output_name = "stardock-manager"
  edition = "2018"

  source_root = "manager/main.rs"
  deps = [
    "//sdk/fidl/fuchsia.stardock:fuchsia.stardock-rustc",
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]

  sources = [
    "manager/main.rs",
    "manager/manager.rs",
  ]
}

fuchsia_component("stardock_component") {
  component_name = "stardock_manager"
  manifest = "meta/stardock_manager.cml"
  deps = [ ":manager_bin" ]
}

fuchsia_package("stardock_package") {
  package_name = "stardock"
  deps = [ ":stardock_component" ]
}

rustc_binary("cli_bin") {
  output_name = "stardock"
  edition = "2018"

  source_root = "cli/main.rs"
  deps = [
    "//sdk/fidl/fuchsia.stardock:fuchsia.stardock-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "cli/main.rs" ]
}

fuchsia_shell_package("stardock_cli") {
  deps = [ ":cli_bin" ]
}

fuchsia_test_package("stardock-tests") {
  test_components = [ ":stardock-common-tests" ]
}
