# Copyright 2021 Fabio D'Urso. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

group("stardock") {
  deps = [
    ":stardock_cli",
    ":stardock_package",
    "//src/proc/bin/starnix",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":stardock-tests" ]
}

rustc_library("common") {
  name = "stardock_common"
  edition = "2018"
  with_unit_tests = true

  source_root = "common/lib.rs"
  deps = [
    "//sdk/fidl/fuchsia.stardock:fuchsia.stardock-rustc",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:serde",
  ]

  test_deps = [
    "//src/lib/fuchsia",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:test-case",
  ]

  sources = [
    "common/digest.rs",
    "common/image_reference.rs",
    "common/lib.rs",
  ]
}

fuchsia_unittest_component("stardock-common-tests") {
  deps = [ ":common_test" ]
}

rustc_binary("manager_bin") {
  output_name = "stardock-manager"
  edition = "2018"

  source_root = "manager/main.rs"
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.stardock:fuchsia.stardock-rustc",
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:flate2",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hex",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:sha2",
    "//third_party/rust_crates:tempfile",
  ]

  sources = [
    "manager/image.rs",
    "manager/main.rs",
    "manager/manager.rs",
    "manager/serde_types.rs",
  ]
}

fuchsia_component("stardock_component") {
  component_name = "stardock_manager"
  manifest = "meta/stardock_manager.cml"
  deps = [ ":manager_bin" ]
}

fuchsia_package("stardock_package") {
  package_name = "stardock"
  deps = [ ":stardock_component" ]
}

rustc_binary("cli_bin") {
  output_name = "stardock"
  edition = "2018"
  with_unit_tests = true

  source_root = "cli/main.rs"
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.net.http:fuchsia.net.http-rustc",
    "//sdk/fidl/fuchsia.stardock:fuchsia.stardock-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:clap",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:serde_json",
  ]

  test_deps = [
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:test-case",
  ]

  sources = [
    "cli/image_fetcher.rs",
    "cli/main.rs",
  ]
}

fuchsia_shell_package("stardock_cli") {
  deps = [ ":cli_bin" ]
}

fuchsia_unittest_component("stardock-cli-tests") {
  deps = [ ":cli_bin_test" ]
}

fuchsia_test_package("stardock-tests") {
  test_components = [
    ":stardock-cli-tests",
    ":stardock-common-tests",
  ]
}
